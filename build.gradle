plugins {
    id 'fabric-loom' version '0.5.12'
    id 'maven-publish'
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }

    maven {
        name = "Ladysnake Libs"
        url = "https://dl.bintray.com/ladysnake/libs"
    }

    maven {
        name = "Fabric Maven"
        url = "https://maven.fabricmc.net"
    }
}

ext.priv = parseConfig(file('private.properties'))

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "io.github.prospector:modmenu:1.14.6+build.31"

    modApi "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:2.5.1"
    modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-level:2.5.1"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

curseforge {
    //if (!priv.hasProperty("curseforge_api_key")) return
    apiKey = priv.curseforge_api_key

    project {
        id = '403888'
        changelog = 'Changes'
        releaseType = 'release'
        addGameVersion(minecraft_version)
        addGameVersion("Java 10")

        mainArtifact remapJar
        addArtifact sourcesJar

        relations {
            requiredDependency 'fabric-api'
            requiredDependency 'cardinal-components'
        }
    }
    options {
        debug = true
        forgeGradleIntegration = false
    }
}

def parseConfig(File config) {
    if(config.exists()){
        config.withReader {
            def prop = new Properties()
            prop.load(it)
            return (new ConfigSlurper().parse(prop))
        }
    } else {
        return null
    }
}